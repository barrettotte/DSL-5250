buildscript{
    repositories{
        mavenCentral()
        maven{
        url "https://plugins.gradle.org/m2/"
      }
    }
}

plugins{
    id 'groovy'
    id 'codenarc'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}


group = 'com.barrettotte'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'
mainClassName = 'com.github.barrettotte.dsl5250.Main'

repositories{
  mavenCentral()
}

dependencies{
  implementation 'org.codehaus.groovy:groovy-all:3.0.3'

  implementation 'net.java.dev.jna:jna:5.5.0'
  implementation 'net.java.dev.jna:jna-platform:5.5.0'

  testImplementation('org.junit.jupiter:junit-jupiter:5.6.2')
}

test{
  useJUnitPlatform()
  testLogging{
        exceptionFormat = 'full'
        showExceptions true
        events 'PASSED', 'FAILED', 'SKIPPED'
        showStandardStreams = true
    }
    afterSuite{ desc, result ->
        if(!desc.parent){
            println "\nTest result: ${result.resultType}"
            println "Test summary: " +
              "${result.testCount} tests, " +
              "${result.successfulTestCount} succeeded, " +
              "${result.failedTestCount} failed, " +
              "${result.skippedTestCount} skipped"
        }
    }
}

[codenarcMain, codenarcTest]*.ignoreFailures = true

codenarc{
  configFile = file("config/codenarc/codenarc.xml")
  reportFormat = 'html'
}
