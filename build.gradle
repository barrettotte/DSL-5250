import java.util.zip.ZipFile

plugins{
  id 'groovy'
  id 'codenarc'
  id 'eclipse'
  id 'idea'
  id 'application' // TODO: temp
  id 'com.github.johnrengelman.shadow' version '5.2.0'
}

ext{
  tn5250Version = '0.7.6'
  tn5250Jar = "libs/tn5250j-${tn5250Version}/tn5250j.jar"
}

group = 'com.github.barrettotte'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'
mainClassName = 'com.github.barrettotte.dsl5250.Main' // TODO: temp

repositories{
  mavenCentral()
  maven{
    url "https://plugins.gradle.org/m2/"
  }
}

dependencies{
  implementation 'org.codehaus.groovy:groovy-all:3.0.3'
  implementation fileTree(dir: 'libs', include: '*.jar')
  implementation 'log4j:log4j:1.2.17'

  testImplementation 'org.junit.jupiter:junit-jupiter:5.6.2'
  testImplementation 'org.mockito:mockito-core:2.27.0'
  testImplementation 'org.mockito:mockito-junit-jupiter:2.27.0'
}

sourceSets{
  main{
    java.srcDirs   = ['src/main/java']
    groovy.srcDirs = ['src/main/groovy']
  }
  test{
    java.srcDirs   = ['src/test/java']
    groovy.srcDirs = ['src/test/groovy']
  }
}

test{
  useJUnitPlatform()
  testLogging{
    exceptionFormat = 'full'
    showExceptions true
    events 'PASSED', 'FAILED', 'SKIPPED'
    showStandardStreams = true
  }
  afterSuite{desc, result ->
    if(!desc.parent){
      println "\nTest result: ${result.resultType}"
      println "Test summary: " +
        "${result.testCount} tests, " +
        "${result.successfulTestCount} succeeded, " +
        "${result.failedTestCount} failed, " +
        "${result.skippedTestCount} skipped"
    }
  }
}

[codenarcMain, codenarcTest]*.ignoreFailures = true

codenarc{
  configFile = file('config/codenarc/codenarc.xml')
  reportFormat = 'html'
}

